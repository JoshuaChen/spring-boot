plugins {
	id 'java'
	id 'org.springframework.boot' version '{version}'
}

subprojects {
	apply plugin: 'java'
	group = 'org.example.projects'
	version = '1.2.3'
	if (it.name == 'bravo') {
		dependencies {
			implementation(project(':charlie'))
		}
	}
}

bootJar {
	mainClass = 'com.example.Application'
	layered {
		application {
			intoLayer("static") {
				include "META-INF/resources/**", "resources/**", "static/**", "public/**"
			}
			intoLayer("app")
		}
		dependencies {
			intoLayer("snapshot-dependencies") {
				include "*:*:*SNAPSHOT"
				excludeProjectDependencies()
			}
			intoLayer("subproject-dependencies") {
				includeProjectDependencies()
			}
			intoLayer("commons-dependencies") {
				include "org.apache.commons:*" 
			}
			intoLayer("dependencies")
		}
		layerOrder = ["dependencies", "commons-dependencies", "snapshot-dependencies", "subproject-dependencies", "static", "app"]
	}
}

repositories {
	mavenCentral()
	maven {
		url = 'repository'
	}
}

dependencies {
	implementation(project(':alpha'))
	implementation(project(':bravo'))
	implementation("com.example:library:1.0-SNAPSHOT")
	implementation("org.apache.commons:commons-lang3:{commonsLang3Version}")
	implementation("org.springframework:spring-core:{springFrameworkVersion}")
}

task listLayers(type: JavaExec) {
	classpath = bootJar.outputs.files
	systemProperties = [ "jarmode": "tools" ]
	args "list-layers"
}

task extractLayers(type: JavaExec) {
	classpath = bootJar.outputs.files
	systemProperties = [ "jarmode": "tools" ]
	args "extract", "--layers", "--launcher", "--destination", ".", "--force"
}
